#!/usr/bin/env perl6

my @selected-benchmarks = ("01-logmunge", "02-gauss-elimination", "03-mandelbrot", "04-backend");

my $fh = open("out", :w);

$fh.say("# timestamp                           version" 
    ~ @selected-benchmarks.map({sprintf("%15s", $_.substr(0, 14));}).join(""));
say("# timestamp                           version" 
    ~ @selected-benchmarks.map({sprintf("%15s %15s %15s", $_.substr(0, 14), "- count", "- stddev");}).join(""));

for dir(test => /'.log' $/).sort.reverse -> $file {
    my $state = 0;
    my $effective-date = Nil;
    my %bench-results = %();
    my $bench-name;
    my $version = $file;
    $version ~~ s:g/'-'/_/;
    $version ~~ s:g/'.log'$//;
    for $file.IO.lines -> $line {
        if $state == 0 && $line ~~ /^ 'commit '/ {
            $state = 1;
        }
        elsif $state == 1 && $line ~~ /^ 'Date:' \s+ (.*)/ {
            my $date-str = $0;
            # argh, git and perl seem to have different ideas about iso 8601...
            $date-str ~~ s/' '/T/;
            $date-str ~~ s/' '//;
            my $date = DateTime.new($date-str);
            if (! defined $effective-date) || ($date > $effective-date) {
                $effective-date = $date;
            }
        }
        elsif $state == 1 && $line ~~ /^ \s* $/ {
            $state = 0;
        }
        elsif ($state == 0 || $state == 2) && $line ~~ /^ '# running ' (.*) '...' / {
            $bench-name = $0;
            $state = 2;
        }
        elsif $state == 2 && $line ~~ /^ 'benchmark complete' / {
            $state = 3;
        }
        elsif $state == 3 && $line ~~ /^ 'real' \s+ (\d+) 'm' (<[\d]+[.]>+) / {
            %bench-results{$bench-name}.push($0 * 60 + $1);
            $state = 2;
        }
    }
    my $result-line = sprintf("%11s", $effective-date.posix);
    $result-line ~= sprintf("%34s", $version);
    for @selected-benchmarks -> $bm {
        if ! defined %bench-results{$bm} {
            $result-line ~= sprintf("%15s %15s %15s", "??", "??", "??");
        }
        else {
            my $sum = [+] %bench-results{$bm};
            my $count = %bench-results{$bm}.elems;
            my $avg = $sum / $count;
            my $devsum = [+] %bench-results{$bm}.map({ ($_ - $avg) ** 2 });
            my $stddev = sqrt($devsum / ($count - 1));
            $result-line ~= sprintf("%15.2f %15d %15.2f", $avg, $count, $stddev.Num);
        }
    }
    $fh.say($result-line);
    say($result-line);
}
$fh.close;
shell("./plot.r");
unlink("out");

$fh = open("README.md", :w);
$fh.say(q:to/END/);
# Yet Another Perl 6 Benchmark

## Goals

This project is trying to write another benchmark for the Perl 6 runtime. I am massively impressed by the work at http://tux.nl/Talks/CSV6/speed4.html, but would also be interested how current runtime versions perform for other workloads.

The specific goals of this becnhmark are:

* Allow comparison of recent versions of the Perl 6 runtime
* Broken down into a few sub-benchmarks focusing on different aspects of the language
* Have non-trivial cases inspired by real-world loads, so less synthetic benchmarks
* Avoid non-core modules so that the benchmark compares runtime performance and not improvements in these modules

## Status and Todos

This is in a very early state, amongst other thigns it needs:

* Fixes to make the "backend" test reliable and meaningful
* Use actual Complex in 03-mandelbrot
* More sub-benchmarks for other aspects of the language
* Reviews of the existing benchmarks to make them more idomatic (not to optimize them in terms of performance)

Please help!

## The Benchmarks

### 01-logmunge

This benchmark simulates logfile analysis, so primarily regular expression matching and other text processing.

### 02-gauss-elimination

This benchmark solves a large system of linear equation using exact maths, focusing on rational number performance.

### 03-mandelbrot

This is a simplistic implementation of a mandelbrot image generator. It is a much more small-scale benchmark than the others, and focuses on iteration and floating point performance.

### 04-backend 

This emulates a service-oriented backend by setting up multiple servers that communicate over a HTTP-like protocol. It involves concurrency, promises, network I/O and text processing. This benchmark is currently broken and very unreliable, any help welcome.

## Results

Below are the results, lower numbers are better of course.

![alt text](bench.svg)

The results for the currently broken backend benchmark:

![alt text](bench-broken.svg)

### Test System

These tests were run on a 8-core Xeon E3-1245v3 at 3.40GHz with 32GB of memory. 

END

$fh.close;
